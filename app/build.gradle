apply plugin: 'com.android.application'
apply from: "$rootDir/release.gradle"

def keystorePropertiesFile = rootProject.file("keystore.properties")
def keystoreProperties = new Properties()
keystoreProperties.load(new FileInputStream(keystorePropertiesFile))

android {
    signingConfigs {
        release_config {
            keyAlias keystoreProperties['keyAlias']
            keyPassword keystoreProperties['keyPassword']
            storeFile file(keystoreProperties['storeFile'])
            storePassword keystoreProperties['storePassword']
        }
    }
    compileSdkVersion 29
    defaultConfig {
        applicationId "alektas.stroymat"
        minSdkVersion 19
        targetSdkVersion 29
        multiDexEnabled true
        versionCode 9
        versionName "0.8.1"
        project.ext.set("archivesBaseName", "Stroymat-" + defaultConfig.versionName)

        resConfigs "en", "ru"

        testInstrumentationRunner "androidx.test.runner.AndroidJUnitRunner"
        vectorDrawables.useSupportLibrary = true

        javaCompileOptions {
            annotationProcessorOptions {
                arguments = ["room.schemaLocation": "$projectDir/schemas".toString()]
            }
        }
    }
    buildTypes {
        release {
            minifyEnabled true
            shrinkResources true
            proguardFiles getDefaultProguardFile('proguard-android-optimize.txt'), 'proguard-rules.pro'
            signingConfig signingConfigs.release_config

            buildConfigField 'String', 'AUTHORITY', '"alektas.stroymat.ItemsProvider"'
            resValue 'string', 'search_authority', 'alektas.stroymat.ItemsProvider'
        }
        debug {
            aaptOptions.cruncherEnabled = false
            minifyEnabled false
            shrinkResources false
            ext.alwaysUpdateBuildId = false
            ext.enableCrashlytics = false
            splits.abi.enable = false
            splits.density.enable = false

            applicationIdSuffix ".debug"
            versionNameSuffix "-debug"

            buildConfigField 'String', 'AUTHORITY', '"alektas.stroymat.debug.ItemsProvider"'
            resValue 'string', 'search_authority', 'alektas.stroymat.debug.ItemsProvider'
        }
    }

    flavorDimensions "mode"
    productFlavors {
        user {
            dimension "mode"
        }

        admin {
            dimension "mode"
            applicationIdSuffix ".admin"
            versionNameSuffix "-admin"
        }
    }

    packagingOptions {
        exclude 'META-INF/LICENSE'
        exclude 'META-INF/LICENSE-FIREBASE.txt'
        exclude 'META-INF/NOTICE'
    }
    compileOptions {
        sourceCompatibility = '1.8'
        targetCompatibility = '1.8'
    }
    buildToolsVersion = '29.0.2'
    sourceSets {
        main { assets.srcDirs = ['src/main/assets', 'src/main/assets/'] }
        // Override the icons in debug mode
        userDebug.res.srcDir 'src/userDebug/res'
        adminDebug.res.srcDir 'src/adminDebug/res'
    }
}

dependencies {
    implementation fileTree(dir: 'libs', include: ['*.jar'])

    implementation 'androidx.multidex:multidex:2.0.1'
    implementation 'androidx.appcompat:appcompat:1.2.0-alpha02'
    implementation 'com.google.android.material:material:1.2.0-alpha05'
    implementation 'androidx.constraintlayout:constraintlayout:2.0.0-beta4'
    implementation 'androidx.legacy:legacy-support-v4:1.0.0'
    implementation 'androidx.lifecycle:lifecycle-extensions:2.2.0'
    implementation 'androidx.recyclerview:recyclerview:1.1.0'

    implementation 'androidx.room:room-runtime:2.2.4'
    annotationProcessor 'androidx.room:room-compiler:2.2.4'

    implementation 'androidx.navigation:navigation-fragment:2.2.1'
    implementation 'androidx.navigation:navigation-ui:2.2.1'

    implementation 'com.google.dagger:dagger:2.26'
    annotationProcessor 'com.google.dagger:dagger-compiler:2.26'

    implementation 'com.github.bumptech.glide:glide:4.9.0'
    annotationProcessor 'com.github.bumptech.glide:compiler:4.9.0'

    implementation 'com.github.chrisbanes:PhotoView:2.3.0'

    implementation 'com.github.luongvo:GmailBackground:2.1.1'

    // Firebase
    implementation 'com.google.firebase:firebase-core:17.2.2'
    implementation 'com.google.firebase:firebase-inappmessaging-display:19.0.3'
    implementation 'com.google.firebase:firebase-messaging:20.1.0'
    implementation 'com.google.firebase:firebase-storage:19.1.1'
    implementation 'com.firebaseui:firebase-ui-storage:4.3.1'
    implementation 'com.google.firebase:firebase-firestore:21.4.0'
    implementation 'com.google.firebase:firebase-config:19.1.1'

    // Testing
    testImplementation 'junit:junit:4.13'
    androidTestImplementation 'androidx.test:runner:1.2.0'
    androidTestImplementation 'androidx.test.espresso:espresso-core:3.2.0'
}
apply plugin: 'com.google.gms.google-services'
